StartLog(LogFile="results/test-AwsS3-CopyObjects-Wildcard.tstool.log")
# Test copying file objects:
# - use the test.openwaterfoundation.org bucket
# - use the bucket folder tstool-aws-plugin/test/commands/AwsS3/test-AwsS3-CopyObjects
# - use wildcard for the destination
#
# Configuration data.
SetProperty(PropertyName="TestS3Folder",PropertyType=String,PropertyValue="tstool-aws-plugin/test/commands/AwsS3/test-AwsS3-CopyObjects")
#
# Remove the bucket files first:
# - list the folder to make sure the files were deleted before uploading and copying
AwsS3(S3Command="DeleteObjects",Bucket="test.openwaterfoundation.org",DeleteFolders="${TestS3Folder}/",DeleteFoldersScope="AllFilesAndFolders")
AwsS3(S3Command="ListObjects",Bucket="test.openwaterfoundation.org",ListObjectsScope=All,Prefix="${TestS3Folder}/",ListObjectsCountProperty="TestCopyListCount",OutputTableID="TestCopyList")
If(Name="IfDeleteCheck",Condition="${TestCopyListCount} != 0")
Message(Message="The copy test files were not deleted for setup.  Expecting 0 but ${TestCopyListCount} remain.",CommandStatus=FAILURE)
EndIf(Name="IfDeleteCheck")
#
# Upload the files for the test that will be the source of the copy.
AwsS3(S3Command="UploadObjects",Region="us-west-2",Bucket="test.openwaterfoundation.org",CopyObjectsCountProperty="TestCopyObjectsCount",UploadFiles="data/testfolder1/testfolder1-testfile1.txt:${TestS3Folder}/*,data/testfolder1/testfolder1-testfile2.txt:${TestS3Folder}/*",OutputTableID="BucketList")
#
# Copy the objects:
# - check the copy count
# - also do a list below to check the count a second way
AwsS3(S3Command="CopyObjects",Bucket="test.openwaterfoundation.org",CopyFiles="${TestS3Folder}/testfolder1-testfile1.txt:${TestS3Folder}/copy/*,${TestS3Folder}/testfolder1-testfile2.txt:${TestS3Folder}/copy/*",CopyObjectsCountProperty="TestCopyObjectsCount")
If(Name="IfCount",Condition="${TestCopyObjectsCount} != 2")
Message(Message="Error - should be 2 if the object is copied (${TestCopyObjectsCount} remain).",CommandStatus=FAILURE)
EndIf(Name="IfCount")
#
# Check to see if the copied objects exists.
AwsS3(S3Command="ListObjects",Bucket="test.openwaterfoundation.org",ListObjectsScope=All,Prefix="${TestS3Folder}/copy",ListObjectsRegEx="*copy*",ListObjectsCountProperty="TestCopyListCount",OutputTableID="TestCopyList")
If(Name="IfTestCopyListCount",Condition="${TestCopyListCount} != 2")
Message(Message="Error - should be 2 if the object is copied (${TestCopyListCount} remain).",CommandStatus=FAILURE)
EndIf(Name="IfTestCopyListCount")
